buildscript { 
	repositories {
		mavenCentral()
		maven {
			url "https://repo.comm-unity.at/nexus/content/groups/public"
			content { includeGroupByRegex "at\\.comm_unity.*" }
		}
	}
	dependencies {
		classpath 'at.comm_unity.gradle:publishing-plugin:1.1.0'
	}
}

plugins {
	id 'eclipse'
	id 'groovy'
}
apply plugin: 'at.comm_unity.maven-publish-conventions'

println "Running Gradle ${gradle.gradleVersion} for ${project.name} with Java " + System.getProperty("java.version") + " (" + System.getProperty("java.home") + ")" 

group = 'at.comm_unity.com.github.jacobono'

java {
    toolchain {
		languageVersion = JavaLanguageVersion.of(8)
    }
	withSourcesJar()
}	

repositories { 
	mavenCentral()
}

sourceSets {
	integTest {
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}
}

configurations {
	integTestImplementation.extendsFrom testImplementation
	integTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
	implementation localGroovy() //'org.codehaus.groovy:groovy-all:3.0.21'
	implementation 'org.slf4j:slf4j-api:1.7.36'
	implementation 'com.google.inject:guice:3.0'
	implementation 'com.google.inject.extensions:guice-assistedinject:3.0'
  
	testImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
	testImplementation ("org.spockframework:spock-core") {
		exclude group: "org.codehaus.groovy", module: "groovy"
	}
	testImplementation 'cglib:cglib-nodep:2.2'
	testImplementation 'org.objenesis:objenesis:1.2'

	integTestImplementation ("org.spockframework:spock-guice") {
		exclude group: "org.codehaus.groovy", module: "groovy"
	}
}

groovydoc {
	docTitle = "Schema Slurping Plugin API"
	link("http://groovy.codehaus.org/gapi/", "groovy.", "org.codehaus.groovy.")
	link("https://docs.oracle.com/javase/8/docs/api/", "java.")
	link("http://google-guice.googlecode.com/svn/trunk/javadoc/", "com.google.")
}

task groovyDocJar(type: Jar, dependsOn: groovydoc) {
	archiveClassifier = 'groovydoc'
	from groovydoc.destinationDir
}

artifacts {
	// to also generate the groovyDocJar at assemble
	archives jar, groovyDocJar
}

task integTest(type: Test, dependsOn: 'test') {
	testClassesDirs = sourceSets.integTest.output.classesDirs
	classpath = sourceSets.integTest.runtimeClasspath
	reports.html.outputLocation = java.testReportDir.dir("integTests")
}

tasks.named('check') {
	dependsOn(integTest)
}

tasks.withType(Test).configureEach {
	useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact groovyDocJar
		}
	}
}

wrapper {
    gradleVersion = '8.6'
}